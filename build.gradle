buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0"
        classpath "pl.allegro.tech.build:axion-release-plugin:1.12.1"
    }
    gradle.startParameter.continueOnFailure = true
}

apply plugin: "org.sonarqube"
apply plugin: 'pl.allegro.tech.build.axion-release'

allprojects {

    group = 'it.cambi'
    version = scmVersion.version

    repositories {
        mavenLocal()
        maven {
            url = 'https://repo.maven.apache.org/maven2'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

configure(subprojects) {

    apply plugin: 'java'
    apply plugin: "jacoco"

    sourceCompatibility = '1.11'
    targetCompatibility = '1.11'
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.6'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'

        implementation 'org.slf4j:slf4j-simple:1.7.5'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.5.15'

        testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
        testImplementation 'javax.xml.bind:jaxb-api:2.3.0'
        testImplementation 'org.springframework:spring-test:5.1.2.RELEASE'
    }

    test {
        useJUnitPlatform()

        jacoco.destinationFile = file("$buildDir/coverage-reports/jacoco-ut.exec")
        finalizedBy jacocoTestReport

        testLogging {
            events "started", "passed", "skipped", "failed"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat
        }
    }
}
